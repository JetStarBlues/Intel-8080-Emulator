   1 REM http://www.ittybittycomputers.com/IttyBitty/TinyBasic/
   2 REM Simplified PRINT statements
 900 PRINT
 901 PRINT
 902 PRINT
1000 PRINT "THE KINGDOM OF EUPHORIA"
1010 PRINT
1020 GOSUB 3510
1100 REM PRINT STATE OF THE KINGDOM
1110 PRINT
1120 PRINT "YEAR:"
1121 PRINT Y/7
1130 PRINT "--POPULATION--"
1131 PRINT "INITIAL"
1132 PRINT P
1140 IF B>0 PRINT "BIRTHS"
1141 IF B>0 PRINT B
1150 IF D>0 PRINT "DEATHS BY NATURAL CAUSES"
1151 IF D>0 PRINT D
1160 IF S>0 PRINT "DEATHS BY STARVATION"
1161 IF S>0 PRINT S
1170 IF K>0 PRINT "WAR CASUALTIES"
1171 IF K>0 PRINT K
1180 IF V>0 PRINT "VICTIMS OF DISEASE"
1181 IF V>0 PRINT V
1190 IF M>0 PRINT "VICTIMS OF LOOTING"
1191 IF M>0 PRINT M
1210 LET D=D+S+K+V+M
1220 LET P=P+B-D
1230 IF B+D>0 PRINT "FINAL"
1231 IF B+D>0 PRINT P
1250 PRINT "--LAND (ACRES)--"
1251 PRINT "INITIAL"
1252 PRINT A
1260 IF T=0 IF U=0 GOTO 1350
1270 IF T>0 PRINT "ACRES BOUGHT"
1271 IF T>0 PRINT T
1280 IF T<0 PRINT "ACRES SOLD"
1281 IF T<0 PRINT -T
1290 IF U#0 PRINT "FRUITS OF WAR"
1292 IF U#0 PRINT U
1310 LET A=A+T+U
1320 LET T=0
1330 LET U=0
1340 PRINT "FINAL"
1341 PRINT A
1350 PRINT "--GRAIN (BUSHELS)--"
1351 PRINT "INITIAL"
1352 PRINT G
1360 IF C<0 GOTO 1530
1370 IF C=0 GOTO 1430
1380 LET R=X
1390 PRINT "CROP YIELD"
1391 PRINT C
1392 PRINT "AT"
1410 GOSUB 3220
1420 REM PRINT
1430 IF F>0 PRINT "BUSHELS USED FOR FOOD"
1431 IF F>0 PRINT F
1440 IF Q>0 PRINT "BUSHELS PLANTED"
1441 IF Q>0 PRINT Q
1450 IF L#0 PRINT "LAND DEALS"
1452 IF L#0 PRINT L
1460 IF H>0 PRINT "MERCENARY HIRE"
1461 IF H>0 PRINT H
1470 IF E>0 PRINT "BUSHELS LOST TO RATS"
1471 IF E>0  PRINT E
1480 IF Z#0 PRINT "FRUITS OF WAR"
1482 IF Z#0 PRINT Z
1490 IF O>0 PRINT "LOOTING LOSSES:"
1492 IF O>0 PRINT O
1510 LET G=G+C-F-Q+L-H-E+Z-O
1520 PRINT "FINAL"
1521 PRINT G
1522 PRINT
1530 GOSUB 3310
1600 REM NOTICE IF GAME ENDED
1610 IF J<100 GOTO 1690
1620 PRINT "THE PEASANTS TIRE OF WAR AND STARVATION YOU ARE DEPOSED."
1640 LET N=Y-1
1650 INPUT "DO YOU WISH TO PLAY AGAIN"N
1670 IF Y#N STOP
1680 GOTO 1000
1690 IF P>1 GOTO 1750
1710 PRINT "YOU AND THE REMAINING POPULATION RETIRE IN THE SWISS ALPS."
1730 GOTO 1640
1740 REM MAKE LAND DEALS
1750 LET R=23+RND(8)
1760 GOSUB 3250
1770 PRINT "BUY AT"
1780 GOSUB 3220
1790 INPUT T
1810 IF T<0 GOTO 1760
1820 IF T=0 GOTO 1860
1830 IF R*T<=G GOTO 2060
1840 GOSUB 3270
1850 GOTO 1760
1860 LET R=R-1
1880 GOSUB 3250
1890 PRINT "SELL AT"
1910 GOSUB 3220
1920 INPUT T
1930 IF T<0 GOTO 1880
1940 IF T=0 GOTO 2060
1950 IF T<=A GOTO 1990
1960 PRINT "BUT THERE IS INSUFFICIENT LAND"
1970 GOTO 1880
1990 IF T<A/10 GOTO 2050
2010 LET R=R-1
2020 PRINT "FOR SELLING SO MUCH YOU CAN ONLY GET"
2030 GOSUB 3220
2040 PRINT
2050 LET T=-T
2060 LET L=-R*T
2100 REM DISTRIBUTE GRAIN
2110 GOSUB 3250
2120 INPUT "PLANT"Q
2140 IF Q<0 GOTO 2110
2150 IF Q<=A+T GOTO 2180
2160 GOSUB 3270
2170 GOTO 2110
2180 IF Q<=P*10 GOTO 2220
2190 PRINT "BUT THERE ARE INSUFFICIENT PEOPLE"
2210 GOTO 2110
2220 INPUT "HOW MANY BUSHELS DO YOU WISH TO USE AS FOOD"F
2250 IF F<0 GOTO 2220
2260 IF G+L-Q-F>=0 GOTO 2310
2270 GOSUB 3270
2280 GOTO 2220
2310 IF F<=40*P LET S=P-F/40
2320 LET J=J+S
2330 LET X=5+RND(4)
2340 IF Y/7*8-Y/49*7=Y LET X=X/2-1
2350 LET C=X*Q
2360 IF G+L-Q-F+C>=0 GOTO 2390
2370 LET X=(32767-G-L+Q+F)/Q
2380 GOTO 2350
2390 IF RND(99)<25 LET E=(G+L-F+C)/10
2400 REM WAR AND PLAGUE
2410 IF RND(99)>15 GOTO 2880
2420 LET W=25
2430 PRINT "A NEARBY KINGDOM THREATENS WAR"
2440 LET N=Y-1
2450 INPUT "DO YOU WISH TO MAKE A PRE-EMPTIVE STRIKE"N
2480 IF N=Y-1 GOTO 2520
2490 LET W=100
2510 LET J=J+5
2520 INPUT "HOW MANY MERCENARIES WILL YOU HIRE AT 80 BUSHELS EACH"N
2550 IF N<0 GOTO 2520
2560 LET D=G+L-Q-F+C-E
2570 IF RND(99)<W GOTO 2610
2580 PRINT "PEACE NEGOTIATIONS SUCCEED"
2590 GOTO 2810
2610 IF W=25 LET W=150
2620 LET R=P-S
2630 IF N>R/10 GOTO 2660
2640 LET R=3*W/5*N*N/R*100/R
2650 GOTO 2680
2660 LET R=3*W/5+N*100/R
2680 LET K=(P-S)/2
2690 LET U=-(A+T)/2
2710 LET Z=-D/2
2720 IF R>RND(99) GOTO 2760
2730 PRINT "YOU HAVE LOST THE WAR"
2740 LET J=J+5000/W
2750 GOTO 2810
2760 PRINT "YOU HAVE WON THE WAR"
2770 LET K=K/2
2780 LET U=-U
2790 LET Z=D/4
2810 IF N*80<=D+Z GOTO 2870
2820 GOSUB 3280
2830 PRINT "TO PAY THE MERCENARIES"
2840 LET M=3*(P-S-K)/4
2850 LET O=3*(R+Z)/4
2860 GOTO 2880
2870 LET H=N*80
2880 LET N=P-S-K-M
2890 IF RND(99)>4 GOTO 2940
2910 PRINT "THE BLACK PLAGUE STRIKES"
2920 LET V=N/2
2930 GOTO 2970
2940 IF RND(99)>20 GOTO 2980
2950 PRINT "A POX EPIDEMIC BREAKS OUT"
2960 LET V=N/20
2970 LET N=N-V
2980 LET B=(N*RND(5)+9)/100+1
2990 LET D=(N*RND(3)+4)/100
3010 IF Y/7*8-Y/49*7#Y GOTO 3030
3020 PRINT "SEVEN YEAR LOCUSTS REDUCE CROP YIELD"
3030 IF E>0 PRINT "RATS INFEST YOUR SILOS"
3040 LET Y=Y+8-(Y-Y/7*7)/6
3050 GOTO 1110
3200 REM UTILITY PRINT ROUTINES
3220 PRINT "BUSHELS/ACRE"
3221 PRINT R
3230 RETURN
3250 PRINT "HOW MANY ACRES DO YOU WISH TO "
3260 RETURN
3270 PRINT "BUT THERE IS INSUFFICIENT GRAIN"
3290 RETURN
3300 REM INITIALIZE POPULATION AND GRAIN VECTORS
3310 LET B=0
3320 LET D=0
3330 LET K=0
3340 LET M=0
3350 LET S=0
3360 LET V=0
3410 LET C=0
3420 LET E=0
3430 LET F=0
3440 LET H=0
3450 LET L=0
3460 LET O=0
3470 LET Q=0
3480 LET Z=0
3490 RETURN
3500 REM INITIALIZATION
3510 LET Y=RND(6)+1
3520 LET J=0
3530 LET P=100
3540 LET A=1500
3550 LET G=5000
3560 LET T=0
3570 LET U=0
3580 GOSUB 3310
3590 LET C=-1
3600 REM PRINT INSTRUCTIONS (MAY BE OMITTED)
3610 LET N=P
3620 INPUT "DO YOU NEED INSTRUCTIONS (Y OR N)"N
3640 IF N=P RETURN
3650 IF N=Y GOTO 3710
3660 PRINT "PLEASE TYPE Y FOR YES, N FOR NO. OK"
3670 GOTO 3620
3710 PRINT "YOU HAVE INHERITED THE THRONE TO THE MEDIEVAL"
3720 PRINT "KINGDOM OF EUPHORIA, SOMEWHERE IN EUROPE."
3730 PRINT "AT THIS TIME IT IS ABOUT"
3731 PRINT A
3732 PRINT "ACRES IN SIZE,"
3740 PRINT "WITH"
3741 PRINT P
3742 PRINT "LOYAL PEASANTS TO SERVE YOU."
3750 PRINT "IN YOUR ROYAL SILOS YOU HAVE"
3751 PRINT G
3752 PRINT "BUSHELS OF NUTRITIOUS GRAIN WITH WHICH TO FEED THE PEOPLE"
3770 PRINT "AND TRANSACT INTERNATIONAL TRADE."
3780 PRINT "CLOSE BY YOUR SIDE ARE JEALOUS NEIGHBORING"
3790 PRINT "KINGDOMS, BUT YOU CAN PROTECT YOURSELF BY"
3810 PRINT "HIRING EVIL MERCENARIES."
3820 PRINT "EACH YEAR YOUR MINISTERS WILL PRESENT YOU"
3830 PRINT "WITH A SUMMARY OF YOUR CURRENT STATUS,"
3840 PRINT "AND THEN ASK YOU FOR DECISIONS ON WHAT"
3850 PRINT "TO DO FOR THE NEXT YEAR. PLEASE TYPE Y OR N"
3860 PRINT "FOR YES OR NO, OR A WHOLE NUMBER FOR NUMERIC ANSWERS."
3920 LET N=P
3930 INPUT "ARE YOU READY, YOUR HIGHNESS"N
3950 IF N#Y GOTO 3920
3980 RETURN
3990 STOP
